<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.HITs.Cost.Base</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Symbol">#-}</a>
<a id="24" class="Keyword">module</a> <a id="31" href="Cubical.HITs.Cost.Base.html" class="Module">Cubical.HITs.Cost.Base</a> <a id="54" class="Keyword">where</a>

<a id="61" class="Keyword">open</a> <a id="66" class="Keyword">import</a> <a id="73" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a>
<a id="101" class="Keyword">open</a> <a id="106" class="Keyword">import</a> <a id="113" href="Cubical.Foundations.Function.html" class="Module">Cubical.Foundations.Function</a>
<a id="142" class="Keyword">open</a> <a id="147" class="Keyword">import</a> <a id="154" href="Cubical.HITs.PropositionalTruncation.html" class="Module">Cubical.HITs.PropositionalTruncation</a>
<a id="191" class="Keyword">open</a> <a id="196" class="Keyword">import</a> <a id="203" href="Cubical.Data.Nat.html" class="Module">Cubical.Data.Nat</a>
<a id="220" class="Keyword">open</a> <a id="225" class="Keyword">import</a> <a id="232" href="Cubical.Data.Sigma.html" class="Module">Cubical.Data.Sigma</a>

<a id="252" class="Keyword">private</a>
  <a id="262" class="Keyword">variable</a>
    <a id="275" href="Cubical.HITs.Cost.Base.html#275" class="Generalizable">ℓ</a> <a id="277" class="Symbol">:</a> <a id="279" href="Agda.Primitive.html#597" class="Postulate">Level</a>
    <a id="289" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a> <a id="291" href="Cubical.HITs.Cost.Base.html#291" class="Generalizable">B</a> <a id="293" href="Cubical.HITs.Cost.Base.html#293" class="Generalizable">C</a> <a id="295" class="Symbol">:</a> <a id="297" href="Agda.Primitive.html#326" class="Primitive">Type</a> <a id="302" href="Cubical.HITs.Cost.Base.html#275" class="Generalizable">ℓ</a>

<a id="Cost"></a><a id="305" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="310" class="Symbol">:</a> <a id="312" class="Symbol">(</a><a id="313" href="Cubical.HITs.Cost.Base.html#313" class="Bound">A</a> <a id="315" class="Symbol">:</a> <a id="317" href="Agda.Primitive.html#326" class="Primitive">Type</a> <a id="322" href="Cubical.HITs.Cost.Base.html#275" class="Generalizable">ℓ</a><a id="323" class="Symbol">)</a> <a id="325" class="Symbol">→</a> <a id="327" href="Agda.Primitive.html#326" class="Primitive">Type</a> <a id="332" href="Cubical.HITs.Cost.Base.html#275" class="Generalizable">ℓ</a>
<a id="334" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="339" href="Cubical.HITs.Cost.Base.html#339" class="Bound">A</a> <a id="341" class="Symbol">=</a> <a id="343" href="Cubical.HITs.Cost.Base.html#339" class="Bound">A</a> <a id="345" href="Cubical.Data.Sigma.Base.html#461" class="Function Operator">×</a> <a id="347" href="Cubical.HITs.PropositionalTruncation.Base.html#249" class="Datatype Operator">∥</a> <a id="349" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="351" href="Cubical.HITs.PropositionalTruncation.Base.html#249" class="Datatype Operator">∥</a>

<a id="354" class="Comment">-- To compare two elements of Cost A we only need to look at the first parts</a>
<a id="Cost≡"></a><a id="431" href="Cubical.HITs.Cost.Base.html#431" class="Function">Cost≡</a> <a id="437" class="Symbol">:</a> <a id="439" class="Symbol">(</a><a id="440" href="Cubical.HITs.Cost.Base.html#440" class="Bound">x</a> <a id="442" href="Cubical.HITs.Cost.Base.html#442" class="Bound">y</a> <a id="444" class="Symbol">:</a> <a id="446" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="451" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a><a id="452" class="Symbol">)</a> <a id="454" class="Symbol">→</a> <a id="456" href="Cubical.HITs.Cost.Base.html#440" class="Bound">x</a> <a id="458" class="Symbol">.</a><a id="459" href="Agda.Builtin.Sigma.html#252" class="Field">fst</a> <a id="463" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="465" href="Cubical.HITs.Cost.Base.html#442" class="Bound">y</a> <a id="467" class="Symbol">.</a><a id="468" href="Agda.Builtin.Sigma.html#252" class="Field">fst</a> <a id="472" class="Symbol">→</a> <a id="474" href="Cubical.HITs.Cost.Base.html#440" class="Bound">x</a> <a id="476" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="478" href="Cubical.HITs.Cost.Base.html#442" class="Bound">y</a>
<a id="480" href="Cubical.HITs.Cost.Base.html#431" class="Function">Cost≡</a> <a id="486" class="Symbol">_</a> <a id="488" class="Symbol">_</a> <a id="490" class="Symbol">=</a> <a id="492" href="Cubical.Data.Sigma.Properties.html#12694" class="Function">Σ≡Prop</a> <a id="499" class="Symbol">λ</a> <a id="501" href="Cubical.HITs.Cost.Base.html#501" class="Bound">_</a> <a id="503" class="Symbol">→</a> <a id="505" href="Cubical.HITs.PropositionalTruncation.Base.html#305" class="InductiveConstructor">squash</a>

<a id="513" class="Comment">-- To make it easier to program with Cost A we prove that it forms a</a>
<a id="582" class="Comment">-- monad which counts the number of calls to &gt;&gt;=. We could also turn</a>
<a id="651" class="Comment">-- it into a proper state monad which tracks the cost, but for the</a>
<a id="718" class="Comment">-- programs we write later this simple version suffices</a>
<a id="_&gt;&gt;=_"></a><a id="774" href="Cubical.HITs.Cost.Base.html#774" class="Function Operator">_&gt;&gt;=_</a> <a id="780" class="Symbol">:</a> <a id="782" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="787" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a> <a id="789" class="Symbol">→</a> <a id="791" class="Symbol">(</a><a id="792" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a> <a id="794" class="Symbol">→</a> <a id="796" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="801" href="Cubical.HITs.Cost.Base.html#291" class="Generalizable">B</a><a id="802" class="Symbol">)</a> <a id="804" class="Symbol">→</a> <a id="806" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="811" href="Cubical.HITs.Cost.Base.html#291" class="Generalizable">B</a>
<a id="813" class="Symbol">(</a><a id="814" href="Cubical.HITs.Cost.Base.html#814" class="Bound">x</a> <a id="816" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="818" href="Cubical.HITs.Cost.Base.html#818" class="Bound">m</a><a id="819" class="Symbol">)</a> <a id="821" href="Cubical.HITs.Cost.Base.html#774" class="Function Operator">&gt;&gt;=</a> <a id="825" href="Cubical.HITs.Cost.Base.html#825" class="Bound">g</a> <a id="827" class="Keyword">with</a> <a id="832" href="Cubical.HITs.Cost.Base.html#825" class="Bound">g</a> <a id="834" href="Cubical.HITs.Cost.Base.html#814" class="Bound">x</a>
<a id="836" class="Symbol">...</a> <a id="840" class="Symbol">|</a> <a id="842" class="Symbol">(</a><a id="843" href="Cubical.HITs.Cost.Base.html#843" class="Bound">y</a> <a id="845" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="847" href="Cubical.HITs.Cost.Base.html#847" class="Bound">n</a><a id="848" class="Symbol">)</a> <a id="850" class="Symbol">=</a> <a id="852" class="Symbol">(</a><a id="853" href="Cubical.HITs.Cost.Base.html#843" class="Bound">y</a> <a id="855" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="857" href="Cubical.HITs.PropositionalTruncation.Properties.html#3174" class="Function">map</a> <a id="861" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="865" class="Symbol">(</a><a id="866" href="Cubical.HITs.PropositionalTruncation.Properties.html#3236" class="Function">map2</a> <a id="871" href="Agda.Builtin.Nat.html#325" class="Primitive Operator">_+_</a> <a id="875" class="Bound">m</a> <a id="877" href="Cubical.HITs.Cost.Base.html#847" class="Bound">n</a><a id="878" class="Symbol">))</a>

<a id="return"></a><a id="882" href="Cubical.HITs.Cost.Base.html#882" class="Function">return</a> <a id="889" class="Symbol">:</a> <a id="891" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a> <a id="893" class="Symbol">→</a> <a id="895" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="900" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a>
<a id="902" href="Cubical.HITs.Cost.Base.html#882" class="Function">return</a> <a id="909" href="Cubical.HITs.Cost.Base.html#909" class="Bound">x</a> <a id="911" class="Symbol">=</a> <a id="913" class="Symbol">(</a><a id="914" href="Cubical.HITs.Cost.Base.html#909" class="Bound">x</a> <a id="916" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="918" href="Cubical.HITs.PropositionalTruncation.Base.html#287" class="InductiveConstructor Operator">∣</a> <a id="920" class="Number">0</a> <a id="922" href="Cubical.HITs.PropositionalTruncation.Base.html#287" class="InductiveConstructor Operator">∣</a><a id="923" class="Symbol">)</a>

<a id="926" class="Comment">-- The monad laws all hold by Cost≡</a>

<a id="&gt;&gt;=-return"></a><a id="963" href="Cubical.HITs.Cost.Base.html#963" class="Function">&gt;&gt;=-return</a> <a id="974" class="Symbol">:</a> <a id="976" class="Symbol">(</a><a id="977" href="Cubical.HITs.Cost.Base.html#977" class="Bound">f</a> <a id="979" class="Symbol">:</a> <a id="981" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="986" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a><a id="987" class="Symbol">)</a> <a id="989" class="Symbol">→</a> <a id="991" href="Cubical.HITs.Cost.Base.html#977" class="Bound">f</a> <a id="993" href="Cubical.HITs.Cost.Base.html#774" class="Function Operator">&gt;&gt;=</a> <a id="997" href="Cubical.HITs.Cost.Base.html#882" class="Function">return</a> <a id="1004" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="1006" href="Cubical.HITs.Cost.Base.html#977" class="Bound">f</a>
<a id="1008" href="Cubical.HITs.Cost.Base.html#963" class="Function">&gt;&gt;=-return</a> <a id="1019" href="Cubical.HITs.Cost.Base.html#1019" class="Bound">f</a> <a id="1021" class="Symbol">=</a> <a id="1023" href="Cubical.HITs.Cost.Base.html#431" class="Function">Cost≡</a> <a id="1029" class="Symbol">_</a> <a id="1031" class="Symbol">_</a> <a id="1033" href="Cubical.Foundations.Prelude.html#889" class="Function">refl</a>

<a id="return-&gt;&gt;="></a><a id="1039" href="Cubical.HITs.Cost.Base.html#1039" class="Function">return-&gt;&gt;=</a> <a id="1050" class="Symbol">:</a> <a id="1052" class="Symbol">(</a><a id="1053" href="Cubical.HITs.Cost.Base.html#1053" class="Bound">a</a> <a id="1055" class="Symbol">:</a> <a id="1057" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a><a id="1058" class="Symbol">)</a> <a id="1060" class="Symbol">(</a><a id="1061" href="Cubical.HITs.Cost.Base.html#1061" class="Bound">f</a> <a id="1063" class="Symbol">:</a> <a id="1065" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a> <a id="1067" class="Symbol">→</a> <a id="1069" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="1074" href="Cubical.HITs.Cost.Base.html#291" class="Generalizable">B</a><a id="1075" class="Symbol">)</a> <a id="1077" class="Symbol">→</a> <a id="1079" href="Cubical.HITs.Cost.Base.html#882" class="Function">return</a> <a id="1086" href="Cubical.HITs.Cost.Base.html#1053" class="Bound">a</a> <a id="1088" href="Cubical.HITs.Cost.Base.html#774" class="Function Operator">&gt;&gt;=</a> <a id="1092" href="Cubical.HITs.Cost.Base.html#1061" class="Bound">f</a> <a id="1094" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="1096" href="Cubical.HITs.Cost.Base.html#1061" class="Bound">f</a> <a id="1098" href="Cubical.HITs.Cost.Base.html#1053" class="Bound">a</a>
<a id="1100" href="Cubical.HITs.Cost.Base.html#1039" class="Function">return-&gt;&gt;=</a> <a id="1111" href="Cubical.HITs.Cost.Base.html#1111" class="Bound">a</a> <a id="1113" href="Cubical.HITs.Cost.Base.html#1113" class="Bound">f</a> <a id="1115" class="Symbol">=</a> <a id="1117" href="Cubical.HITs.Cost.Base.html#431" class="Function">Cost≡</a> <a id="1123" class="Symbol">_</a> <a id="1125" class="Symbol">_</a> <a id="1127" href="Cubical.Foundations.Prelude.html#889" class="Function">refl</a>

<a id="&gt;&gt;=-assoc"></a><a id="1133" href="Cubical.HITs.Cost.Base.html#1133" class="Function">&gt;&gt;=-assoc</a> <a id="1143" class="Symbol">:</a> <a id="1145" class="Symbol">(</a><a id="1146" href="Cubical.HITs.Cost.Base.html#1146" class="Bound">f</a> <a id="1148" class="Symbol">:</a> <a id="1150" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="1155" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a><a id="1156" class="Symbol">)</a> <a id="1158" class="Symbol">(</a><a id="1159" href="Cubical.HITs.Cost.Base.html#1159" class="Bound">g</a> <a id="1161" class="Symbol">:</a> <a id="1163" href="Cubical.HITs.Cost.Base.html#289" class="Generalizable">A</a> <a id="1165" class="Symbol">→</a> <a id="1167" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="1172" href="Cubical.HITs.Cost.Base.html#291" class="Generalizable">B</a><a id="1173" class="Symbol">)</a> <a id="1175" class="Symbol">(</a><a id="1176" href="Cubical.HITs.Cost.Base.html#1176" class="Bound">h</a> <a id="1178" class="Symbol">:</a> <a id="1180" href="Cubical.HITs.Cost.Base.html#291" class="Generalizable">B</a> <a id="1182" class="Symbol">→</a> <a id="1184" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="1189" href="Cubical.HITs.Cost.Base.html#293" class="Generalizable">C</a><a id="1190" class="Symbol">)</a>
          <a id="1202" class="Symbol">→</a> <a id="1204" class="Symbol">(</a><a id="1205" href="Cubical.HITs.Cost.Base.html#1146" class="Bound">f</a> <a id="1207" href="Cubical.HITs.Cost.Base.html#774" class="Function Operator">&gt;&gt;=</a> <a id="1211" href="Cubical.HITs.Cost.Base.html#1159" class="Bound">g</a><a id="1212" class="Symbol">)</a> <a id="1214" href="Cubical.HITs.Cost.Base.html#774" class="Function Operator">&gt;&gt;=</a> <a id="1218" href="Cubical.HITs.Cost.Base.html#1176" class="Bound">h</a> <a id="1220" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="1222" href="Cubical.HITs.Cost.Base.html#1146" class="Bound">f</a> <a id="1224" href="Cubical.HITs.Cost.Base.html#774" class="Function Operator">&gt;&gt;=</a> <a id="1228" class="Symbol">(λ</a> <a id="1231" href="Cubical.HITs.Cost.Base.html#1231" class="Bound">x</a> <a id="1233" class="Symbol">→</a> <a id="1235" href="Cubical.HITs.Cost.Base.html#1159" class="Bound">g</a> <a id="1237" href="Cubical.HITs.Cost.Base.html#1231" class="Bound">x</a> <a id="1239" href="Cubical.HITs.Cost.Base.html#774" class="Function Operator">&gt;&gt;=</a> <a id="1243" href="Cubical.HITs.Cost.Base.html#1176" class="Bound">h</a><a id="1244" class="Symbol">)</a>
<a id="1246" href="Cubical.HITs.Cost.Base.html#1133" class="Function">&gt;&gt;=-assoc</a> <a id="1256" href="Cubical.HITs.Cost.Base.html#1256" class="Bound">f</a> <a id="1258" href="Cubical.HITs.Cost.Base.html#1258" class="Bound">g</a> <a id="1260" href="Cubical.HITs.Cost.Base.html#1260" class="Bound">h</a> <a id="1262" class="Symbol">=</a> <a id="1264" href="Cubical.HITs.Cost.Base.html#431" class="Function">Cost≡</a> <a id="1270" class="Symbol">_</a> <a id="1272" class="Symbol">_</a> <a id="1274" href="Cubical.Foundations.Prelude.html#889" class="Function">refl</a>


<a id="1281" class="Comment">-- An inefficient version of addition which recurses on both arguments</a>
<a id="addBad"></a><a id="1352" href="Cubical.HITs.Cost.Base.html#1352" class="Function">addBad</a> <a id="1359" class="Symbol">:</a> <a id="1361" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="1363" class="Symbol">→</a> <a id="1365" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="1367" class="Symbol">→</a> <a id="1369" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="1374" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a>
<a id="1376" href="Cubical.HITs.Cost.Base.html#1352" class="Function">addBad</a> <a id="1383" class="Number">0</a> <a id="1385" class="Number">0</a>       <a id="1393" class="Symbol">=</a> <a id="1395" href="Cubical.HITs.Cost.Base.html#882" class="Function">return</a> <a id="1402" class="Number">0</a>
<a id="1404" href="Cubical.HITs.Cost.Base.html#1352" class="Function">addBad</a> <a id="1411" class="Number">0</a> <a id="1413" class="Symbol">(</a><a id="1414" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="1418" href="Cubical.HITs.Cost.Base.html#1418" class="Bound">y</a><a id="1419" class="Symbol">)</a> <a id="1421" class="Symbol">=</a> <a id="1423" class="Keyword">do</a>
  <a id="1428" href="Cubical.HITs.Cost.Base.html#1428" class="Bound">x</a> <a id="1430" href="Cubical.HITs.Cost.Base.html#774" class="Function Operator">←</a> <a id="1432" href="Cubical.HITs.Cost.Base.html#1352" class="Function">addBad</a> <a id="1439" class="Number">0</a> <a id="1441" href="Cubical.HITs.Cost.Base.html#1418" class="Bound">y</a>
  <a id="1445" href="Cubical.HITs.Cost.Base.html#882" class="Function">return</a> <a id="1452" class="Symbol">(</a><a id="1453" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="1457" href="Cubical.HITs.Cost.Base.html#1428" class="Bound">x</a><a id="1458" class="Symbol">)</a>
<a id="1460" href="Cubical.HITs.Cost.Base.html#1352" class="Function">addBad</a> <a id="1467" class="Symbol">(</a><a id="1468" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="1472" href="Cubical.HITs.Cost.Base.html#1472" class="Bound">x</a><a id="1473" class="Symbol">)</a> <a id="1475" href="Cubical.HITs.Cost.Base.html#1475" class="Bound">y</a> <a id="1477" class="Symbol">=</a> <a id="1479" class="Keyword">do</a>
  <a id="1484" href="Cubical.HITs.Cost.Base.html#1484" class="Bound">z</a> <a id="1486" href="Cubical.HITs.Cost.Base.html#774" class="Function Operator">←</a> <a id="1488" href="Cubical.HITs.Cost.Base.html#1352" class="Function">addBad</a> <a id="1495" href="Cubical.HITs.Cost.Base.html#1472" class="Bound">x</a> <a id="1497" href="Cubical.HITs.Cost.Base.html#1475" class="Bound">y</a>
  <a id="1501" href="Cubical.HITs.Cost.Base.html#882" class="Function">return</a> <a id="1508" class="Symbol">(</a><a id="1509" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="1513" href="Cubical.HITs.Cost.Base.html#1484" class="Bound">z</a><a id="1514" class="Symbol">)</a>

<a id="1517" class="Comment">-- More efficient addition which only recurse on the first argument</a>
<a id="add"></a><a id="1585" href="Cubical.HITs.Cost.Base.html#1585" class="Function">add</a> <a id="1589" class="Symbol">:</a> <a id="1591" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="1593" class="Symbol">→</a> <a id="1595" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="1597" class="Symbol">→</a> <a id="1599" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="1604" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a>
<a id="1606" href="Cubical.HITs.Cost.Base.html#1585" class="Function">add</a> <a id="1610" class="Number">0</a>       <a id="1618" href="Cubical.HITs.Cost.Base.html#1618" class="Bound">y</a> <a id="1620" class="Symbol">=</a> <a id="1622" href="Cubical.HITs.Cost.Base.html#882" class="Function">return</a> <a id="1629" href="Cubical.HITs.Cost.Base.html#1618" class="Bound">y</a>
<a id="1631" href="Cubical.HITs.Cost.Base.html#1585" class="Function">add</a> <a id="1635" class="Symbol">(</a><a id="1636" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="1640" href="Cubical.HITs.Cost.Base.html#1640" class="Bound">x</a><a id="1641" class="Symbol">)</a> <a id="1643" href="Cubical.HITs.Cost.Base.html#1643" class="Bound">y</a> <a id="1645" class="Symbol">=</a> <a id="1647" class="Keyword">do</a>
  <a id="1652" href="Cubical.HITs.Cost.Base.html#1652" class="Bound">z</a> <a id="1654" href="Cubical.HITs.Cost.Base.html#774" class="Function Operator">←</a> <a id="1656" href="Cubical.HITs.Cost.Base.html#1585" class="Function">add</a> <a id="1660" href="Cubical.HITs.Cost.Base.html#1640" class="Bound">x</a> <a id="1662" href="Cubical.HITs.Cost.Base.html#1643" class="Bound">y</a>
  <a id="1666" href="Cubical.HITs.Cost.Base.html#882" class="Function">return</a> <a id="1673" class="Symbol">(</a><a id="1674" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="1678" href="Cubical.HITs.Cost.Base.html#1652" class="Bound">z</a><a id="1679" class="Symbol">)</a>

<a id="1682" class="Keyword">private</a>
  <a id="1692" class="Comment">-- addBad x y will do x + y calls</a>
  <a id="1728" href="Cubical.HITs.Cost.Base.html#1728" class="Function">_</a> <a id="1730" class="Symbol">:</a> <a id="1732" href="Cubical.HITs.Cost.Base.html#1352" class="Function">addBad</a> <a id="1739" class="Number">3</a> <a id="1741" class="Number">5</a> <a id="1743" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="1745" class="Symbol">(</a><a id="1746" class="Number">8</a> <a id="1748" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1750" href="Cubical.HITs.PropositionalTruncation.Base.html#287" class="InductiveConstructor Operator">∣</a> <a id="1752" class="Number">8</a> <a id="1754" href="Cubical.HITs.PropositionalTruncation.Base.html#287" class="InductiveConstructor Operator">∣</a><a id="1755" class="Symbol">)</a>
  <a id="1759" class="Symbol">_</a> <a id="1761" class="Symbol">=</a> <a id="1763" href="Cubical.Foundations.Prelude.html#889" class="Function">refl</a>

  <a id="1771" class="Comment">-- add x y will only do x recursive calls</a>
  <a id="1815" href="Cubical.HITs.Cost.Base.html#1815" class="Function">_</a> <a id="1817" class="Symbol">:</a> <a id="1819" href="Cubical.HITs.Cost.Base.html#1585" class="Function">add</a> <a id="1823" class="Number">3</a> <a id="1825" class="Number">5</a> <a id="1827" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="1829" class="Symbol">(</a><a id="1830" class="Number">8</a> <a id="1832" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1834" href="Cubical.HITs.PropositionalTruncation.Base.html#287" class="InductiveConstructor Operator">∣</a> <a id="1836" class="Number">3</a> <a id="1838" href="Cubical.HITs.PropositionalTruncation.Base.html#287" class="InductiveConstructor Operator">∣</a><a id="1839" class="Symbol">)</a>
  <a id="1843" class="Symbol">_</a> <a id="1845" class="Symbol">=</a> <a id="1847" href="Cubical.Foundations.Prelude.html#889" class="Function">refl</a>

<a id="1853" class="Comment">-- Despite addBad and add having different cost we can still prove</a>
<a id="1920" class="Comment">-- that they are equal functions</a>
<a id="add≡addBad"></a><a id="1953" href="Cubical.HITs.Cost.Base.html#1953" class="Function">add≡addBad</a> <a id="1964" class="Symbol">:</a> <a id="1966" href="Cubical.HITs.Cost.Base.html#1585" class="Function">add</a> <a id="1970" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="1972" href="Cubical.HITs.Cost.Base.html#1352" class="Function">addBad</a>
<a id="1979" href="Cubical.HITs.Cost.Base.html#1953" class="Function">add≡addBad</a> <a id="1990" href="Cubical.HITs.Cost.Base.html#1990" class="Bound">i</a> <a id="1992" href="Cubical.HITs.Cost.Base.html#1992" class="Bound">x</a> <a id="1994" href="Cubical.HITs.Cost.Base.html#1994" class="Bound">y</a> <a id="1996" class="Symbol">=</a> <a id="1998" href="Cubical.HITs.Cost.Base.html#431" class="Function">Cost≡</a> <a id="2004" class="Symbol">(</a><a id="2005" href="Cubical.HITs.Cost.Base.html#1585" class="Function">add</a> <a id="2009" href="Cubical.HITs.Cost.Base.html#1992" class="Bound">x</a> <a id="2011" href="Cubical.HITs.Cost.Base.html#1994" class="Bound">y</a><a id="2012" class="Symbol">)</a> <a id="2014" class="Symbol">(</a><a id="2015" href="Cubical.HITs.Cost.Base.html#1352" class="Function">addBad</a> <a id="2022" href="Cubical.HITs.Cost.Base.html#1992" class="Bound">x</a> <a id="2024" href="Cubical.HITs.Cost.Base.html#1994" class="Bound">y</a><a id="2025" class="Symbol">)</a> <a id="2027" class="Symbol">(</a><a id="2028" href="Cubical.HITs.Cost.Base.html#2049" class="Function">rem</a> <a id="2032" href="Cubical.HITs.Cost.Base.html#1992" class="Bound">x</a> <a id="2034" href="Cubical.HITs.Cost.Base.html#1994" class="Bound">y</a><a id="2035" class="Symbol">)</a> <a id="2037" href="Cubical.HITs.Cost.Base.html#1990" class="Bound">i</a>
  <a id="2041" class="Keyword">where</a>
  <a id="2049" href="Cubical.HITs.Cost.Base.html#2049" class="Function">rem</a> <a id="2053" class="Symbol">:</a> <a id="2055" class="Symbol">(</a><a id="2056" href="Cubical.HITs.Cost.Base.html#2056" class="Bound">x</a> <a id="2058" href="Cubical.HITs.Cost.Base.html#2058" class="Bound">y</a> <a id="2060" class="Symbol">:</a> <a id="2062" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a><a id="2063" class="Symbol">)</a> <a id="2065" class="Symbol">→</a> <a id="2067" href="Cubical.HITs.Cost.Base.html#1585" class="Function">add</a> <a id="2071" href="Cubical.HITs.Cost.Base.html#2056" class="Bound">x</a> <a id="2073" href="Cubical.HITs.Cost.Base.html#2058" class="Bound">y</a> <a id="2075" class="Symbol">.</a><a id="2076" href="Agda.Builtin.Sigma.html#252" class="Field">fst</a> <a id="2080" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="2082" href="Cubical.HITs.Cost.Base.html#1352" class="Function">addBad</a> <a id="2089" href="Cubical.HITs.Cost.Base.html#2056" class="Bound">x</a> <a id="2091" href="Cubical.HITs.Cost.Base.html#2058" class="Bound">y</a> <a id="2093" class="Symbol">.</a><a id="2094" href="Agda.Builtin.Sigma.html#252" class="Field">fst</a>
  <a id="2100" href="Cubical.HITs.Cost.Base.html#2049" class="Function">rem</a> <a id="2104" class="Number">0</a> <a id="2106" class="Number">0</a>       <a id="2114" class="Symbol">=</a> <a id="2116" href="Cubical.Foundations.Prelude.html#889" class="Function">refl</a>
  <a id="2123" href="Cubical.HITs.Cost.Base.html#2049" class="Function">rem</a> <a id="2127" class="Number">0</a> <a id="2129" class="Symbol">(</a><a id="2130" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="2134" href="Cubical.HITs.Cost.Base.html#2134" class="Bound">y</a><a id="2135" class="Symbol">)</a> <a id="2137" class="Symbol">=</a> <a id="2139" href="Cubical.Foundations.Prelude.html#1125" class="Function">cong</a> <a id="2144" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="2148" class="Symbol">(</a><a id="2149" href="Cubical.HITs.Cost.Base.html#2049" class="Function">rem</a> <a id="2153" class="Number">0</a> <a id="2155" href="Cubical.HITs.Cost.Base.html#2134" class="Bound">y</a><a id="2156" class="Symbol">)</a>
  <a id="2160" href="Cubical.HITs.Cost.Base.html#2049" class="Function">rem</a> <a id="2164" class="Symbol">(</a><a id="2165" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="2169" href="Cubical.HITs.Cost.Base.html#2169" class="Bound">x</a><a id="2170" class="Symbol">)</a> <a id="2172" href="Cubical.HITs.Cost.Base.html#2172" class="Bound">y</a> <a id="2174" class="Symbol">=</a> <a id="2176" href="Cubical.Foundations.Prelude.html#1125" class="Function">cong</a> <a id="2181" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="2185" class="Symbol">(</a><a id="2186" href="Cubical.HITs.Cost.Base.html#2049" class="Function">rem</a> <a id="2190" href="Cubical.HITs.Cost.Base.html#2169" class="Bound">x</a> <a id="2192" href="Cubical.HITs.Cost.Base.html#2172" class="Bound">y</a><a id="2193" class="Symbol">)</a>


<a id="2197" class="Comment">-- Another example: computing Fibonacci numbers</a>

<a id="fib"></a><a id="2246" href="Cubical.HITs.Cost.Base.html#2246" class="Function">fib</a> <a id="2250" class="Symbol">:</a> <a id="2252" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="2254" class="Symbol">→</a> <a id="2256" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="2261" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a>
<a id="2263" href="Cubical.HITs.Cost.Base.html#2246" class="Function">fib</a> <a id="2267" class="Number">0</a> <a id="2269" class="Symbol">=</a> <a id="2271" href="Cubical.HITs.Cost.Base.html#882" class="Function">return</a> <a id="2278" class="Number">0</a>
<a id="2280" href="Cubical.HITs.Cost.Base.html#2246" class="Function">fib</a> <a id="2284" class="Number">1</a> <a id="2286" class="Symbol">=</a> <a id="2288" href="Cubical.HITs.Cost.Base.html#882" class="Function">return</a> <a id="2295" class="Number">1</a>
<a id="2297" href="Cubical.HITs.Cost.Base.html#2246" class="Function">fib</a> <a id="2301" class="Symbol">(</a><a id="2302" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="2306" class="Symbol">(</a><a id="2307" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="2311" href="Cubical.HITs.Cost.Base.html#2311" class="Bound">x</a><a id="2312" class="Symbol">))</a> <a id="2315" class="Symbol">=</a> <a id="2317" class="Keyword">do</a>
  <a id="2322" href="Cubical.HITs.Cost.Base.html#2322" class="Bound">y</a> <a id="2324" href="Cubical.HITs.Cost.Base.html#774" class="Function Operator">←</a> <a id="2326" href="Cubical.HITs.Cost.Base.html#2246" class="Function">fib</a> <a id="2330" class="Symbol">(</a><a id="2331" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="2335" href="Cubical.HITs.Cost.Base.html#2311" class="Bound">x</a><a id="2336" class="Symbol">)</a>
  <a id="2340" href="Cubical.HITs.Cost.Base.html#2340" class="Bound">z</a> <a id="2342" href="Cubical.HITs.Cost.Base.html#774" class="Function Operator">←</a> <a id="2344" href="Cubical.HITs.Cost.Base.html#2246" class="Function">fib</a> <a id="2348" href="Cubical.HITs.Cost.Base.html#2311" class="Bound">x</a>
  <a id="2352" href="Cubical.HITs.Cost.Base.html#882" class="Function">return</a> <a id="2359" class="Symbol">(</a><a id="2360" href="Cubical.HITs.Cost.Base.html#2322" class="Bound">y</a> <a id="2362" href="Agda.Builtin.Nat.html#325" class="Primitive Operator">+</a> <a id="2364" href="Cubical.HITs.Cost.Base.html#2340" class="Bound">z</a><a id="2365" class="Symbol">)</a>

<a id="fibTail"></a><a id="2368" href="Cubical.HITs.Cost.Base.html#2368" class="Function">fibTail</a> <a id="2376" class="Symbol">:</a> <a id="2378" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="2380" class="Symbol">→</a> <a id="2382" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="2387" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a>
<a id="2389" href="Cubical.HITs.Cost.Base.html#2368" class="Function">fibTail</a> <a id="2397" class="Number">0</a>       <a id="2405" class="Symbol">=</a> <a id="2407" href="Cubical.HITs.Cost.Base.html#882" class="Function">return</a> <a id="2414" class="Number">0</a>
<a id="2416" href="Cubical.HITs.Cost.Base.html#2368" class="Function">fibTail</a> <a id="2424" class="Symbol">(</a><a id="2425" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="2429" href="Cubical.HITs.Cost.Base.html#2429" class="Bound">x</a><a id="2430" class="Symbol">)</a> <a id="2432" class="Symbol">=</a> <a id="2434" href="Cubical.HITs.Cost.Base.html#2457" class="Function">fibAux</a> <a id="2441" href="Cubical.HITs.Cost.Base.html#2429" class="Bound">x</a> <a id="2443" class="Number">1</a> <a id="2445" class="Number">0</a>
  <a id="2449" class="Keyword">where</a>
  <a id="2457" href="Cubical.HITs.Cost.Base.html#2457" class="Function">fibAux</a> <a id="2464" class="Symbol">:</a> <a id="2466" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="2468" class="Symbol">→</a> <a id="2470" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="2472" class="Symbol">→</a> <a id="2474" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="2476" class="Symbol">→</a> <a id="2478" href="Cubical.HITs.Cost.Base.html#305" class="Function">Cost</a> <a id="2483" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a>
  <a id="2487" href="Cubical.HITs.Cost.Base.html#2457" class="Function">fibAux</a> <a id="2494" class="Number">0</a> <a id="2496" href="Cubical.HITs.Cost.Base.html#2496" class="Bound">res</a> <a id="2500" class="Symbol">_</a>          <a id="2511" class="Symbol">=</a> <a id="2513" href="Cubical.HITs.Cost.Base.html#882" class="Function">return</a> <a id="2520" href="Cubical.HITs.Cost.Base.html#2496" class="Bound">res</a>
  <a id="2526" href="Cubical.HITs.Cost.Base.html#2457" class="Function">fibAux</a> <a id="2533" class="Symbol">(</a><a id="2534" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="2538" href="Cubical.HITs.Cost.Base.html#2538" class="Bound">x</a><a id="2539" class="Symbol">)</a> <a id="2541" href="Cubical.HITs.Cost.Base.html#2541" class="Bound">res</a> <a id="2545" href="Cubical.HITs.Cost.Base.html#2545" class="Bound">prev</a> <a id="2550" class="Symbol">=</a> <a id="2552" class="Keyword">do</a>
    <a id="2559" href="Cubical.HITs.Cost.Base.html#2559" class="Bound">r</a> <a id="2561" href="Cubical.HITs.Cost.Base.html#774" class="Function Operator">←</a> <a id="2563" href="Cubical.HITs.Cost.Base.html#2457" class="Function">fibAux</a> <a id="2570" href="Cubical.HITs.Cost.Base.html#2538" class="Bound">x</a> <a id="2572" class="Symbol">(</a><a id="2573" href="Cubical.HITs.Cost.Base.html#2541" class="Bound">res</a> <a id="2577" href="Agda.Builtin.Nat.html#325" class="Primitive Operator">+</a> <a id="2579" href="Cubical.HITs.Cost.Base.html#2545" class="Bound">prev</a><a id="2583" class="Symbol">)</a> <a id="2585" href="Cubical.HITs.Cost.Base.html#2541" class="Bound">res</a>
    <a id="2593" href="Cubical.HITs.Cost.Base.html#882" class="Function">return</a> <a id="2600" href="Cubical.HITs.Cost.Base.html#2559" class="Bound">r</a>


<a id="2604" class="Keyword">private</a>
  <a id="2614" href="Cubical.HITs.Cost.Base.html#2614" class="Function">_</a> <a id="2616" class="Symbol">:</a> <a id="2618" href="Cubical.HITs.Cost.Base.html#2246" class="Function">fib</a> <a id="2622" class="Number">10</a> <a id="2625" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="2627" class="Symbol">(</a><a id="2628" class="Number">55</a> <a id="2631" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="2633" href="Cubical.HITs.PropositionalTruncation.Base.html#287" class="InductiveConstructor Operator">∣</a> <a id="2635" class="Number">176</a> <a id="2639" href="Cubical.HITs.PropositionalTruncation.Base.html#287" class="InductiveConstructor Operator">∣</a><a id="2640" class="Symbol">)</a>
  <a id="2644" class="Symbol">_</a> <a id="2646" class="Symbol">=</a> <a id="2648" href="Cubical.Foundations.Prelude.html#889" class="Function">refl</a>

  <a id="2656" href="Cubical.HITs.Cost.Base.html#2656" class="Function">_</a> <a id="2658" class="Symbol">:</a> <a id="2660" href="Cubical.HITs.Cost.Base.html#2368" class="Function">fibTail</a> <a id="2668" class="Number">10</a> <a id="2671" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="2673" class="Symbol">(</a><a id="2674" class="Number">55</a> <a id="2677" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="2679" href="Cubical.HITs.PropositionalTruncation.Base.html#287" class="InductiveConstructor Operator">∣</a> <a id="2681" class="Number">9</a> <a id="2683" href="Cubical.HITs.PropositionalTruncation.Base.html#287" class="InductiveConstructor Operator">∣</a><a id="2684" class="Symbol">)</a>
  <a id="2688" class="Symbol">_</a> <a id="2690" class="Symbol">=</a> <a id="2692" href="Cubical.Foundations.Prelude.html#889" class="Function">refl</a>

  <a id="2700" href="Cubical.HITs.Cost.Base.html#2700" class="Function">_</a> <a id="2702" class="Symbol">:</a> <a id="2704" href="Cubical.HITs.Cost.Base.html#2246" class="Function">fib</a> <a id="2708" class="Number">20</a> <a id="2711" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="2713" class="Symbol">(</a><a id="2714" class="Number">6765</a> <a id="2719" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="2721" href="Cubical.HITs.PropositionalTruncation.Base.html#287" class="InductiveConstructor Operator">∣</a> <a id="2723" class="Number">21890</a> <a id="2729" href="Cubical.HITs.PropositionalTruncation.Base.html#287" class="InductiveConstructor Operator">∣</a><a id="2730" class="Symbol">)</a>
  <a id="2734" class="Symbol">_</a> <a id="2736" class="Symbol">=</a> <a id="2738" href="Cubical.Foundations.Prelude.html#889" class="Function">refl</a>

  <a id="2746" href="Cubical.HITs.Cost.Base.html#2746" class="Function">_</a> <a id="2748" class="Symbol">:</a> <a id="2750" href="Cubical.HITs.Cost.Base.html#2368" class="Function">fibTail</a> <a id="2758" class="Number">20</a> <a id="2761" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="2763" class="Symbol">(</a><a id="2764" class="Number">6765</a> <a id="2769" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="2771" href="Cubical.HITs.PropositionalTruncation.Base.html#287" class="InductiveConstructor Operator">∣</a> <a id="2773" class="Number">19</a> <a id="2776" href="Cubical.HITs.PropositionalTruncation.Base.html#287" class="InductiveConstructor Operator">∣</a><a id="2777" class="Symbol">)</a>
  <a id="2781" class="Symbol">_</a> <a id="2783" class="Symbol">=</a> <a id="2785" href="Cubical.Foundations.Prelude.html#889" class="Function">refl</a>

<a id="2791" class="Comment">-- Exercise left to the reader: prove that fib and fibTail are equal functions</a>
<a id="2870" class="Comment">-- fib≡fibTail : fib ≡ fibTail</a>
<a id="2901" class="Comment">-- fib≡fibTail i x = Cost≡ (fib x) (fibTail x) (rem x) i</a>
<a id="2958" class="Comment">--   where</a>
<a id="2969" class="Comment">--   rem : (x : ℕ) → fib x .fst ≡ fibTail x .fst</a>
<a id="3018" class="Comment">--   rem zero = refl</a>
<a id="3039" class="Comment">--   rem (suc zero) = refl</a>
<a id="3066" class="Comment">--   rem (suc (suc x)) = {!!}</a>
</pre></body></html>