{-# OPTIONS --safe --lossy-unification #-}
module Cubical.Algebra.RingedLattice.Properties where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.HLevels

open import Cubical.Algebra.CommRing
open import Cubical.Algebra.Semilattice
open import Cubical.Algebra.Lattice
open import Cubical.Algebra.DistLattice

open import Cubical.Categories.Functor

open import Cubical.Categories.Instances.CommRings
open import Cubical.Categories.Instances.DistLattice

open import Cubical.Relation.Binary.Order.Poset

open import Cubical.Algebra.RingedLattice.Base

private
  variable
    â„“ â„“' â„“'' : Level

module _ where
  open Functor
  open RingedLattice
  open RingedLatticeHom


  idRingedLatticeHom : (X : RingedLattice â„“) â†’ RingedLatticeHom X X
  Ï€ (idRingedLatticeHom X) = idDistLatticeHom (X .L)
  Ï€â™¯ (idRingedLatticeHom X) u = idCommRingHom (X .ğ“• .F-ob u)
  isNatÏ€â™¯ (idRingedLatticeHom X) u v uâ‰¥v =
    idCommRingHom (X .ğ“• .F-ob v) âˆ˜cr X .ğ“• .F-hom uâ‰¥v â‰¡âŸ¨ idCompCommRingHom _ âŸ©
    X .ğ“• .F-hom uâ‰¥v â‰¡âŸ¨ cong (X .ğ“• .F-hom) (is-prop-valued _ _ _ _) âŸ©
    X .ğ“• .F-hom (presâ‰¤ (idDistLatticeHom (X .L)) uâ‰¥v) â‰¡âŸ¨ sym (compIdCommRingHom _) âŸ©
    X .ğ“• .F-hom (presâ‰¤ (idDistLatticeHom (X .L)) uâ‰¥v) âˆ˜cr idCommRingHom (X .ğ“• .F-ob u) âˆ
    where
    open JoinSemilattice (Latticeâ†’JoinSemilattice (DistLatticeâ†’Lattice (X .L)))
    open PosetStr (IndPoset .snd)


  compRingedLatticeHom : (X Y Z : RingedLattice â„“)
                         --(Y : RingedLattice â„“') (Z : RingedLattice â„“'')
                       â†’ RingedLatticeHom X Y â†’ RingedLatticeHom Y Z â†’ RingedLatticeHom X Z
  Ï€ (compRingedLatticeHom X Y Z f g) = compDistLatticeHom _ _ _ (f .Ï€) (g .Ï€)
  Ï€â™¯ (compRingedLatticeHom X Y Z f g) u = compCommRingHom _ _ _ (f .Ï€â™¯ u)
                                                                (g .Ï€â™¯ (f .Ï€ .fst u))
  isNatÏ€â™¯ (compRingedLatticeHom X Y Z f g) u v uâ‰¥v =
      (g .Ï€â™¯ (f .Ï€ .fst v) âˆ˜cr f .Ï€â™¯ v) âˆ˜cr X .ğ“• .F-hom uâ‰¥v
    â‰¡âŸ¨ sym (compAssocCommRingHom _ _ _) âŸ©
      g .Ï€â™¯ (f .Ï€ .fst v) âˆ˜cr (f .Ï€â™¯ v âˆ˜cr X .ğ“• .F-hom uâ‰¥v)
    â‰¡âŸ¨ cong (g .Ï€â™¯ (f .Ï€ .fst v) âˆ˜cr_) (f .isNatÏ€â™¯ _ _ uâ‰¥v) âŸ©
      g .Ï€â™¯ (f .Ï€ .fst v) âˆ˜cr (Y .ğ“• .F-hom (presâ‰¤ (f .Ï€) uâ‰¥v) âˆ˜cr f .Ï€â™¯ u)
    â‰¡âŸ¨ compAssocCommRingHom _ _ _ âŸ©
      (g .Ï€â™¯ (f .Ï€ .fst v) âˆ˜cr Y .ğ“• .F-hom (presâ‰¤ (f .Ï€) uâ‰¥v)) âˆ˜cr f .Ï€â™¯ u
    â‰¡âŸ¨ cong (_âˆ˜cr (f .Ï€â™¯ u)) (g .isNatÏ€â™¯ _ _ (presâ‰¤ (f .Ï€) uâ‰¥v)) âŸ©
      (Z .ğ“• .F-hom (presâ‰¤ (g .Ï€) (presâ‰¤ (f .Ï€) uâ‰¥v)) âˆ˜cr g .Ï€â™¯ (f .Ï€ .fst u)) âˆ˜cr f .Ï€â™¯ u
    â‰¡âŸ¨ sym (compAssocCommRingHom _ _ _) âŸ©
      Z .ğ“• .F-hom (presâ‰¤ (g .Ï€) (presâ‰¤ (f .Ï€) uâ‰¥v)) âˆ˜cr (g .Ï€â™¯ (f .Ï€ .fst u) âˆ˜cr f .Ï€â™¯ u)
    â‰¡âŸ¨ cong (Î» x â†’ Z .ğ“• .F-hom x âˆ˜cr (g .Ï€â™¯ (f .Ï€ .fst u) âˆ˜cr f .Ï€â™¯ u))
            (is-prop-valued _ _ _ _) âŸ©
      Z .ğ“• .F-hom (presâ‰¤ (g .Ï€ âˆ˜dl f .Ï€) uâ‰¥v) âˆ˜cr (g .Ï€â™¯ (f .Ï€ .fst u) âˆ˜cr f .Ï€â™¯ u) âˆ
    where
    open JoinSemilattice (Latticeâ†’JoinSemilattice (DistLatticeâ†’Lattice (Z .L)))
    open PosetStr (IndPoset .snd)


  _âˆ˜rl_ : {X Y Z : RingedLattice â„“} --{Y : RingedLattice â„“'} {Z : RingedLattice â„“''}
        â†’ RingedLatticeHom Y Z â†’ RingedLatticeHom X Y â†’ RingedLatticeHom X Z
  g âˆ˜rl f = compRingedLatticeHom _ _ _ f g

  compIdRingedLatticeHom : {X Y : RingedLattice â„“} (f : RingedLatticeHom X Y)
                         â†’ compRingedLatticeHom _ _ _ (idRingedLatticeHom X) f â‰¡ f
  compIdRingedLatticeHom _ = RingedLatticeHomâ‰¡ _ _ (compIdDistLatticeHom _)
                                                   (Î» _  â†’ compIdCommRingHom _)

  idCompRingedLatticeHom : {X Y : RingedLattice â„“} (f : RingedLatticeHom X Y)
                         â†’ compRingedLatticeHom _ _ _ f (idRingedLatticeHom Y) â‰¡ f
  idCompRingedLatticeHom _ = RingedLatticeHomâ‰¡ _ _ (idCompDistLatticeHom _)
                                                   (Î» _  â†’ idCompCommRingHom _)

  compAssocRingedLatticeHom : {X Y Z U : RingedLattice â„“}
                              (f : RingedLatticeHom X Y)
                              (g : RingedLatticeHom Y Z)
                              (h : RingedLatticeHom Z U)
                            â†’ compRingedLatticeHom _ _ _ (compRingedLatticeHom _ _ _ f g) h
                            â‰¡ compRingedLatticeHom _ _ _ f (compRingedLatticeHom _ _ _ g h)
  compAssocRingedLatticeHom _ _ _ = RingedLatticeHomâ‰¡ _ _ (compAssocDistLatticeHom _ _ _)
                                                      (Î» _  â†’ compAssocCommRingHom _ _ _)
